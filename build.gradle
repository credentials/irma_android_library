apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

version = "0.13.6"
group = "org.irmacard.android"

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

repositories {
    mavenLocal()
    maven {
        url "https://credentials.github.io/repos/maven2/"
    }
    mavenCentral()
}

dependencies {
    compile('com.google.http-client:google-http-client-android:1.20.0') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile('org.irmacard.api:irma_api_common:0.3.7') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile 'com.android.support:support-v4:23+'
    compile 'com.github.nicolasjafelle:paginglistview:1.2'
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
        }
    }

    lintOptions {
        // Ignore external package references
        disable 'InvalidPackage'
    }

    dexOptions {
        incremental true
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.manifest.srcFile
}

artifacts {
    archives sourcesJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

if ( project.hasProperty("mavenRepositoryIRMA") ) {
    uploadArchives.repositories.mavenDeployer {
        repository(url: mavenRepositoryIRMA)
    }
}
