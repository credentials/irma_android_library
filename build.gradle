apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

version = "0.11.0"
group = "org.irmacard.android"

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

repositories {
    mavenLocal()
    maven {
        url "https://credentials.github.io/repos/maven2/"
    }
    mavenCentral()
}

dependencies {
    compile 'com.android.support:support-v4:19.1.0'
    compile('org.irmacard.api:irma_api_common:0.2.0') {
        // Android has its own HttpClient, so exclude the one that credentials_api_includes
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile("org.irmacard.idemix:idemix_terminal:0.12.0") {
        // Android has its own HttpClient, so exclude the one that credentials_api_includes
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile 'com.github.nicolasjafelle:paginglistview:1.2'
}

android {
    compileSdkVersion 16
    buildToolsVersion "19.1"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
        }
    }

    lintOptions {
        // Ignore external package references
        disable 'InvalidPackage'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

if ( project.hasProperty("mavenRepositoryIRMA") ) {
    uploadArchives.repositories.mavenDeployer {
        repository(url: mavenRepositoryIRMA)
    }
}
